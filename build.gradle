buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Establish extended properties for all projects
apply from: 'versions.gradle'

group = 'org.iplantc.de'

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven {
            url 'https://maven.sencha.com/repo/support-commercial-release'
            credentials {
                // To download the GXT deps, place your Sencha support username and password in your
                //   ~/.gradle/gradle.properties file
                username = "${sencha_support_user}"
                password = "${sencha_support_password}"
            }

        }
    }

    group = 'org.iplantc.de'

    // Exclude all GwtTests from normal java compiles
    test {
        exclude '**/GwtTest**'
        // Disable the test report for the individual test task
        reports.html.enabled = false
        jvmArgs += [ "-XX:MaxPermSize=256m" ]
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task npmInstall(type:Exec) {
    group 'Build'
    description 'Installs the node_modules depedencies in the react-components directory.'
    workingDir 'react-components'
    commandLine 'npm', 'install'
}

task npmTest(type:Exec, dependsOn:npmInstall) {
    group 'Verification'
    description 'Runs the npm tests in the react-components directory (use `npm test` manually in non-CI envs).'
    workingDir 'react-components'
    commandLine 'npm', 'run', 'test:ci'
}

task webpack(type:Exec, dependsOn:npmInstall) {
    group 'Build'
    description 'Builds the react-components directory.'
    workingDir 'react-components'
    commandLine 'npm', 'run', 'build'
}

apply from: 'iplant/webapps.gradle'
apply from: 'iplant/modules.gradle'

dependencies {
    compile project(':de-webapp')
}

clean {
    delete "./target"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

